uses Plasmacore


class RenderTarget
  PROPERTIES
    size                  : XY
    true_size             : XY
    viewport_size         : XY

    target_display_size : XY?
    display_scale       : Real64

    target_anchor = Anchor.TOP_LEFT : Anchor

    offset : XY
    # All drawing is offset by adding this value to the coordinates.
    # All pointer coordinates are offset by subtracting this value.
    # Coordinates are relative to the top-left corner (0,0).
    # (0,0) by default. Automatically set as part of scale_to_fit().

    perspective_center : XY
    # The "origin" for 3D perspective and the vanishing point for polygons
    # parallel to the camera axis.  Automatically reset to Display.center at
    # the beginning of every draw.

    projection_mode   = ProjectionMode2DX() : ProjectionMode

    clip       : Box?
    clip_stack = Box?[]

    projection_transform  : Matrix
    transform             : Matrix
    world_transform       : Matrix  # Combination of view and object transforms
    view_transform        = Matrix.identity : Matrix
    object_transform      = Matrix.identity : Matrix
    view_transforms       = Matrix[]
    object_transforms     = Matrix[]

    transform_modified        : Logical
    view_transform_modified   : Logical
    world_transform_modified : Logical

    is_offscreen          : Logical

    is_id_configured          : Logical
    render_target_id          : Int32

  METHODS
    method init

    method init( render_target_id )
      is_id_configured = true
      is_offscreen = true

    method clear_scale
      local new_display_scale = 1.0
      local new_size = true_size
      local new_offset = XY(0,0)
      if (new_display_scale != display_scale or new_size != size or new_offset != offset)
        Display.flush
        display_scale = new_display_scale
        size = new_size
        offset = new_offset
        projection_mode.update
      endIf

    method clear_transforms
      Display.flush
      object_transforms.clear
      view_transforms.clear
      world_transform_modified = true
      transform_modified = true

    method activate
      native @|glBindFramebuffer( GL_FRAMEBUFFER, $this->render_target_id );
      OpenGL.log_errors( "Binding framebuffer" )

    method configure( true_size )
      configure( true_size, true_size )

    method configure( true_size, viewport_size )
      if (not is_id_configured)
        # is_id_configured is only set when we're drawing to an offscreen buffer.
        # Otherwise we need to fetch the render target ID every frame to be able
        # to recover from screen lock due to timeout or iPad cover.
        native @|GLint default_id;
                |glGetIntegerv( GL_FRAMEBUFFER_BINDING, &default_id );
                |$this->render_target_id = (RogueInt32) default_id;
      endIf

      activate

      clip = null
      clip_stack.clear

      # Ensure viewport min/max ordering matches true size ordering
      if ((true_size.x <= true_size.y) xor (viewport_size.x <= viewport_size.y))
        viewport_size = XY( viewport_size.y, viewport_size.x )
      endIf

      if (target_display_size.exists)
        perspective_center = target_display_size.value / 2
      else
        perspective_center = true_size / 2
      endIf

      clear_transforms
      scale_to_fit( target_display_size, target_anchor )
      <ui_hook>

    method pop_clip
      if (clip_stack.count)
        clip = clip_stack.remove_last
      else
        clip = null
      endIf

    method push_clip( box:Box?, &replace )
      clip_stack.add( clip )

      if (replace and clip.exists and box.exists)
        clip = clip.value & box.value
      else
        clip = box
      endIf

    method push_object_transform( m:Matrix )
      if (object_transforms.count)
        object_transforms.add( object_transforms.last * m )
      else
        object_transforms.add( m )
      endIf
      @object_transform = object_transforms.last
      world_transform_modified = true
      transform_modified = true

    method push_view_transform( m:Matrix )
      if (view_transforms.count)
        view_transforms.add( view_transforms.last * m )
      else
        view_transforms.add( m )
      endIf
      @view_transform = view_transforms.last
      world_transform_modified = true
      transform_modified = true

    method pop_object_transform( n=1:Int32 )
      forEach (1..n.or_smaller(object_transforms.count))
        object_transforms.remove_last
      endForEach
      if (object_transforms.count) object_transform = object_transforms.last
      else                         object_transform = Matrix.identity
      world_transform_modified = true
      transform_modified = true

    method pop_view_transform( n=1:Int32 )
      forEach (1..n.or_smaller(view_transforms.count))
        view_transforms.remove_last
      endForEach
      if (view_transforms.count) view_transform = view_transforms.last
      else                       view_transform = Matrix.identity
      world_transform_modified = true
      transform_modified = true

    method scale_to_fill( w:Int32, h:Int32, target_anchor )
      scale_to_fill( XY(w,h), target_anchor )

    method scale_to_fill( @target_display_size, target_anchor )
      if (not target_display_size.exists) clear_scale; return

      local new_display_scale = (target_display_size.value / true_size).min
      local new_size = true_size * new_display_scale
      local new_offset = (new_size - target_display_size.value) * target_anchor.position
      if (new_display_scale != display_scale or new_size != size or new_offset != offset)
        Display.flush
        display_scale = new_display_scale
        size = new_size
        offset = new_offset
        projection_mode.update
      endIf

    method scale_to_fit( w:Int32, h:Int32, target_anchor )
      scale_to_fit( XY(w,h), target_anchor )

    method scale_to_fit( @target_display_size, target_anchor )
      if (not target_display_size.exists) clear_scale; return

      local new_display_scale = (target_display_size.value / true_size).max
      local new_size = true_size * new_display_scale
      local new_offset = (new_size - target_display_size.value) * target_anchor.position
      if (new_display_scale != display_scale or new_size != size or new_offset != offset)
        Display.flush
        display_scale = new_display_scale
        size = new_size
        offset = new_offset
        projection_mode.update
      endIf

    method set_clip( new_clip:Box? )
      contingent
        # Avoid rendering cached content if the clipping region is unchanged
        sufficient (new_clip.exists != clip.exists)

        # Existing and new clipping region are both null or both non-null.

        # If the CR was null before and is still null, no need to flush the render buffer
        necessary (new_clip.exists)

        # Was non-null before and is non-null now; did the value change?
        necessary (new_clip.value != clip.value)

      satisfied
        Display.flush
      endContingent

      @clip = new_clip

      if (clip.exists)
        local box = (clip.value & Display.bounds)
        if (box.exists) @clip = box
        else            @clip = null
      endIf

      if (clip.exists)
        local scale = viewport_size / size
        local box = clip.value
        local pos = box.position * scale
        local size = box.size * scale

        local w = size.x->Int32
        local h = size.y->Int32
        local x = pos.x->Int32
        local y = (viewport_size.y - (pos.y + h))->Int32

        native @|glEnable( GL_SCISSOR_TEST );
                |glScissor( $x, $y, $w, $h );
      else
        native @|glDisable( GL_SCISSOR_TEST );
      endIf

    method set_offset( new_offset:XY )
      if (new_offset != offset)
        @offset = new_offset
        projection_mode.update
      endIf

    method set_perspective_center( new_center:XY )
      if (new_center != perspective_center)
        @perspective_center = new_center
        projection_mode.update
      endIf

    method transform->Matrix
      if (transform_modified)
        transform_modified = false
        transform = projection_transform * world_transform
      endIf
      return @transform

    method world_transform->Matrix
      if (not world_transform_modified) return @world_transform

      world_transform_modified = false
      if (view_transforms.count)
        if (object_transforms.count)
          world_transform = view_transform * object_transform
        else
          world_transform = view_transform
        endIf
      else
        if (object_transforms.count)
          world_transform = object_transform
        else
          world_transform = Matrix.identity
        endIf
      endIf

      return @world_transform

endClass
